plugins {
    id 'java'
    id 'maven-publish'
}

group 'com.github.mrmks.effekseer4j'
version '0.1.11-SNAPSHOT'

configurations {
    shade
    compileOnly.extendsFrom shade
}

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {

    testCompileOnly 'junit:junit:4.12'

    testImplementation platform("org.lwjgl:lwjgl-bom:3.2.3")

    testImplementation "org.lwjgl:lwjgl"
    testImplementation "org.lwjgl:lwjgl-assimp"
    testImplementation "org.lwjgl:lwjgl-glfw"
    testImplementation "org.lwjgl:lwjgl-openal"
    testImplementation "org.lwjgl:lwjgl-opengl"
    testImplementation "org.lwjgl:lwjgl-stb"
    testImplementation "org.lwjgl:lwjgl::natives-windows"
    testImplementation "org.lwjgl:lwjgl-assimp::natives-windows"
    testImplementation "org.lwjgl:lwjgl-glfw::natives-windows"
    testImplementation "org.lwjgl:lwjgl-openal::natives-windows"
    testImplementation "org.lwjgl:lwjgl-opengl::natives-windows"
    testImplementation "org.lwjgl:lwjgl-stb::natives-windows"

    shade files('libs/effekseer-native-master-SNAPSHOT.jar')
    runtimeOnly files('libs/effekseer-native-master-SNAPSHOT.jar')
}

jar {
    from {
        configurations.shade.collect { it.isDirectory() ? it : zipTree(it) }
    }

    manifest {
        attributes(
                Sealed: true
        )
    }
}

tasks.register('sourceJar', org.gradle.jvm.tasks.Jar) {
    archiveClassifier.set 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        register('product', MavenPublication) {
            artifactId 'effekseer4j'

            from components.java
            artifact sourceJar
        }
    }
}