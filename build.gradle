plugins {
    id 'java'
    id 'maven-publish'
}

group 'com.github.mrmks.effekseer4j'
version '0.1.9-SNAPSHOT'

configurations {
    shade
    compileOnly.extendsFrom shade
}

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {

    testCompileOnly 'junit:junit:4.12'

    testRuntimeOnly platform("org.lwjgl:lwjgl-bom:3.2.3")

    testRuntimeOnly "org.lwjgl:lwjgl"
    testRuntimeOnly "org.lwjgl:lwjgl-assimp"
    testRuntimeOnly "org.lwjgl:lwjgl-glfw"
    testRuntimeOnly "org.lwjgl:lwjgl-openal"
    testRuntimeOnly "org.lwjgl:lwjgl-opengl"
    testRuntimeOnly "org.lwjgl:lwjgl-stb"
    testRuntimeOnly "org.lwjgl:lwjgl::natives-windows"
    testRuntimeOnly "org.lwjgl:lwjgl-assimp::natives-windows"
    testRuntimeOnly "org.lwjgl:lwjgl-glfw::natives-windows"
    testRuntimeOnly "org.lwjgl:lwjgl-openal::natives-windows"
    testRuntimeOnly "org.lwjgl:lwjgl-opengl::natives-windows"
    testRuntimeOnly "org.lwjgl:lwjgl-stb::natives-windows"

    shade files('libs/effekseer-native-master-SNAPSHOT.jar')
    runtimeOnly files('libs/effekseer-native-master-SNAPSHOT.jar')
}

jar {
    from {
        configurations.shade.collect { it.isDirectory() ? it : zipTree(it) }
    }

    manifest {
        attributes(
                Sealed: true
        )
    }
}

tasks.register('sourceJar', org.gradle.jvm.tasks.Jar) {
    archiveClassifier.set 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        register('product', MavenPublication) {
            artifactId 'effekseer4j'

            from components.java
            artifact sourceJar
        }
    }
}